plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

android {
    namespace 'stkim1.view.MTImageMapView'
    compileSdk 34

    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName '0.1'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    android.libraryVariants.configureEach { variant ->
        variant.outputs.configureEach { file ->
            if (file.outputFileName.endsWith('.aar')) {
                file.outputFileName = "${project.name}-${variant.buildType.name}-${android.defaultConfig.versionName}.aar"
            }
        }
    }
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId "stkim1.view"
            artifactId "mtimagemapview"
            version "0.1"
            artifact("$buildDir/outputs/aar/${project.name}-release-${android.defaultConfig.versionName}.aar")
        }
    }
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/stkim1/view.MTImageMapView")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}